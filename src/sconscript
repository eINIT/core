Import(['env', 'InstallDirSbin', 'InstallDirLib', 'InstallDirLibBin', 'InternalModules', 'InstallDirInclude', 'AssemblyDir', 'ASMFunctionsAvailable', 'uname', 'InternalModuleObjects'])

import glob

AssemblyFunctions = []

for i in ASMFunctionsAvailable:
 AssemblyFunctions += [ AssemblyDir + '/' + i + '.S' ]

CodeSet = ['set-lowmem.c', 'set-common.c']
CodeITree = ['itree-trinary-splay.c']
CodeBTree = ['btree-splay.c']
CodeSTree = ['tree-itree.c']

CodeCoreObjects = ['einit-core.c', 'module.c', 'ipc.c']
CodeCoreMonitorObjects = ['einit-monitor.c']

CodeLastRites = ['last-rites.c']
CodeEinitHelper = ['einit-helper.c']

CodeEinitFeedbackD = ['einit-feedback.c']
CodeEinitLogD = ['einit-log.c']

CodeEinitSysVD = ['tools/einit-sysvd.c']
CodeEinitBootChartD = ['tools/einit-bootchartd.c']

CodeLibEinit = AssemblyFunctions + ['libraries/libeinit.c', 'bitch.c', 'event.c', 'utility.c', 'exec.c', 'strings.c', 'sexp.c', 'ipc-client.c'] + CodeSet + CodeSTree + CodeBTree + CodeITree

if uname == 'linux':
 CodeLibEinit += ['linux/process.c']

if uname == 'freebsd':
 CodeCoreMonitorObjects = [ 'freebsd/einit-monitor.c' ]

coreLibs = ['einit']

einitCore = env.Program ('einit-core', CodeCoreObjects + InternalModuleObjects, LIBS=['einit'], LIBPATH=['.'])
if uname != 'freebsd':
 einitMonitor = env.Program ('einit-monitor', CodeCoreMonitorObjects)
if uname == 'freebsd':
 einitMonitor = env.Program ('einit-monitor', CodeCoreMonitorObjects, LIBS=['util'])

lastRites = env.Program ('last-rites', CodeLastRites, LIBS=['einit'], LIBPATH=['.'])
einitHelper = env.Program ('einit-helper', CodeEinitHelper, LIBS=['einit'], LIBPATH=['.'])

einitFeedbackD = env.Program ('einit-feedback', CodeEinitFeedbackD, LIBS=['einit', 'curses'], LIBPATH=['.'])
einitLogD = env.Program ('einit-log', CodeEinitLogD, LIBS=['einit'], LIBPATH=['.'])

einitSysVD = env.Program ('einit-sysvd', CodeEinitSysVD, LIBS=['einit'], LIBPATH=['.'])
einitBootChartD = env.Program ('einit-bootchartd', CodeEinitBootChartD, LIBS=['einit'], LIBPATH=['.'])

env.Install (InstallDirLibBin, env.Program ('einit-wtf', ['tools/einit-wtf.c'], LIBS=['einit'], LIBPATH=['.']))
env.Install (InstallDirLibBin, env.Program ('einit-test',  ['tools/einit-test.c'], LIBS=['einit'], LIBPATH=['.']))

if uname == 'linux':
 libEinit = env.SharedLibrary ('einit', CodeLibEinit, SHLINKFLAGS='-shared -Wl,-soname,libeinit.so.3.0.0')
else:
 libEinit = env.SharedLibrary ('einit', CodeLibEinit)

env.Install (InstallDirLibBin, einitCore)
env.Install (InstallDirLibBin, einitHelper)
env.Install (InstallDirLibBin, lastRites)
env.Install (InstallDirLibBin, einitFeedbackD)

env.Install (InstallDirLibBin, einitLogD)
env.Install (InstallDirLibBin, einitBootChartD)

env.InstallAs (InstallDirSbin + '/einit', einitMonitor)
env.Install (InstallDirLib, libEinit)
env.Install (InstallDirSbin, einitSysVD)

env.Install (InstallDirInclude + '/einit', glob.glob ('include/einit/*'))
env.Install (InstallDirInclude + '/einit-modules', glob.glob ('include/einit-modules/*'))
